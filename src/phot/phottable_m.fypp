! Module  : phottable_m
! Purpose : define phottable_t abstract type, for representing a table
!           of photometric intensities
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module phottable_m

   ! Uses

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, abstract :: phottable_t
   contains
      procedure(inquire_), deferred :: inquire
      #:for name in ('intensity', 'E_moment', 'D_moment', 'flux')
         procedure(interp_${name}$_), deferred :: interp_${name}$
      #:endfor
      procedure(write_), deferred  :: write
      procedure, nopass, public    :: elem_group_name   
   end type phottable_t

   ! Interfaces

   abstract interface

      subroutine inquire_(self, n)
         import phottable_t
         class(phottable_t), intent(in)  :: self
         integer, intent(out), optional  :: n
      end subroutine inquire_

      #:for name, arg_var, arg_type in (('intensity', 'mu', 'real(RD)'), &
                                        ('E_moment', 'k', 'integer'), &
                                        ('D_moment', 'l', 'integer'))

         subroutine interp_${name}$_(self, i, ${arg_var}$, res, stat)
            use forum_m
            import phottable_t
            class(phottable_t), intent(inout) :: self
            integer, intent(in)               :: i
            ${arg_type}$, intent(in)          :: ${arg_var}$
            real(RD), intent(out)             :: res
            integer, intent(out), optional    :: stat
         end subroutine interp_${name}$_

      #:endfor

      #:for name in (('flux'),)

         subroutine interp_${name}$_(self, i, res, stat)
            use forum_m
            import phottable_t
            class(phottable_t), intent(inout) :: self
            integer, intent(in)               :: i
            real(RD), intent(out)             :: res
            integer, intent(out), optional    :: stat
         end subroutine interp_${name}$_

      #:endfor

      subroutine write_(self, hdf5io, stat)
         use forum_m
         import phottable_t
         class(phottable_t), intent(inout) :: self
         type(hdf5io_t), intent(inout)     :: hdf5io
         integer, intent(out), optional    :: stat
      end subroutine write_

   end interface

   ! Access specifiers

   private

   public :: phottable_t

contains

   function elem_group_name(i) result(name)

      integer, intent(in) :: i
      character(256)      :: name

      write(name, 100) i
100   format('photints(',I0,')')
      
   end function elem_group_name

end module phottable_m
