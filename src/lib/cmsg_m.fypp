! Module  : cmsg_m
! Purpose : C public interface for MSG modules
!
! Copyright 2021-2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module cmsg_m

   ! Uses

   use forum_m
   use msg_m

   use ISO_FORTRAN_ENV
   use ISO_C_BINDING

   ! No implicit typing

   implicit none

   ! Access specifiers

   private

   #:for name in ('specgrid', 'photgrid')
      public :: load_${name}$
      public :: unload_${name}$
      public :: inquire_${name}$
      public :: interp_intensity_${name}$
      public :: interp_E_moment_${name}$
      public :: interp_D_moment_${name}$
      public :: interp_flux_${name}$
   #:endfor

   public :: load_photgrid_from_specgrid

   ! Procedures

contains

   #:for name, type in (('specgrid', 'specgrid_t'), ('photgrid', 'photgrid_t'))

   subroutine load_${name}$(${name}$_filename, ${name}$, stat) bind(C)

         character(C_CHAR) :: ${name}$_filename(*)
         type(C_PTR)       :: ${name}$
         integer(C_INT)    :: stat

         type(hdf5io_t)          :: hi
         type(${type}$), pointer :: gr

         ! Load the ${name}$ from file

         call open_file_(${name}$_filename, hi, stat)
         if (stat /= STAT_OK) return

         allocate(gr)

         call read(hi, gr, stat)
         if (STAT /= STAT_OK) then
            deallocate(gr)
            return
         endif

         call hi%final()

         ! Set up the C pointer

         ${name}$ = C_LOC(gr)

         ! Finish

         return

      end subroutine load_${name}$

      !****

      subroutine unload_${name}$(${name}$) bind(C)

         type(C_PTR), value :: ${name}$

         type(${type}$), pointer :: gr

         ! Set up the Fortran pointer

         call C_F_POINTER(${name}$, gr)

         ! Deallocate the grid

         if (ASSOCIATED(gr)) deallocate(gr)

         ! Finish

         return

      end subroutine unload_${name}$

   #:endfor

   !****

   subroutine load_photgrid_from_specgrid(specgrid_filename, passband_filename, photgrid, stat) bind(C)

      character(C_CHAR) :: specgrid_filename(*)
      character(C_CHAR) :: passband_filename(*)
      type(C_PTR)       :: photgrid
      integer(C_INT)    :: stat

      type(hdf5io_t)            :: hi
      type(passband_t)          :: pb
      type(photgrid_t), pointer :: pg

      ! Load the photgrid from specgrid and passband files

      call open_file_(passband_filename, hi, stat)
      if (stat /= STAT_OK) return
      
      call read(hi, pb, stat)
      if (stat /= STAT_OK) return

      call hi%final()

      call open_file_(specgrid_filename, hi, stat)
      if (stat /= STAT_OK) return

      allocate(pg)

      call read(hi, pb, pg, stat)
      if (stat /= STAT_OK) then
         deallocate(pg)
         return
      endif

      call hi%final()

      ! Set up the C pointer

      photgrid = C_LOC(pg)

      ! Finish

      return

   end subroutine load_photgrid_from_specgrid

   !****

   subroutine inquire_specgrid(specgrid, lam_min, lam_max, shape_ptr, rank, axis_min_ptr, axis_max_ptr) bind(C)

      type(C_PTR), value :: specgrid
      real(C_DOUBLE)     :: lam_min
      real(C_DOUBLE)     :: lam_max
      type(C_PTR), value :: shape_ptr
      integer(C_INT)     :: rank
      type(C_PTR), value :: axis_min_ptr
      type(C_PTR), value :: axis_max_ptr

      type(specgrid_t), pointer :: sg
      integer                   :: rank_
      integer, pointer          :: shape(:)
      real(RD), pointer         :: axis_min(:)
      real(RD), pointer         :: axis_max(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(specgrid, sg)

      ! Inquire metadata

      if (C_ASSOCIATED(shape_ptr)) then
         call sg%inquire(rank=rank_)
         call C_F_POINTER(shape_ptr, shape, [rank_])
         call sg%inquire(shape=shape)
      endif

      if (C_ASSOCIATED(axis_min_ptr)) then
         call sg%inquire(rank=rank_)
         call C_F_POINTER(axis_min_ptr, axis_min, [rank_])
         call sg%inquire(axis_min=axis_min)
      endif

      if (C_ASSOCIATED(axis_max_ptr)) then
         call sg%inquire(rank=rank_)
         call C_F_POINTER(axis_max_ptr, axis_max, [rank_])
         call sg%inquire(axis_max=axis_max)
      endif

      call sg%inquire(lam_min=lam_min, lam_max=lam_max, rank=rank)

      ! Finish

      return

   end subroutine inquire_specgrid

   !****

   subroutine inquire_photgrid(photgrid, shape_ptr, rank, axis_min_ptr, axis_max_ptr) bind(C)

      type(C_PTR), value :: photgrid
      type(C_PTR), value :: shape_ptr
      integer(C_INT)     :: rank
      type(C_PTR), value :: axis_min_ptr
      type(C_PTR), value :: axis_max_ptr
      
      type(photgrid_t), pointer :: pg
      integer                   :: rank_
      integer, pointer          :: shape(:)
      real(RD), pointer         :: axis_min(:)
      real(RD), pointer         :: axis_max(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(photgrid, pg)

      ! Inquire metadata

      if (C_ASSOCIATED(shape_ptr)) then
         call pg%inquire(rank=rank_)
         call C_F_POINTER(shape_ptr, shape, [rank_])
         call pg%inquire(shape=shape)
      endif

      if (C_ASSOCIATED(axis_min_ptr)) then
         call pg%inquire(rank=rank_)
         call C_F_POINTER(axis_min_ptr, axis_min, [rank_])
         call pg%inquire(axis_min=axis_min)
      endif

      if (C_ASSOCIATED(axis_max_ptr)) then
         call pg%inquire(rank=rank_)
         call C_F_POINTER(axis_max_ptr, axis_max, [rank_])
         call pg%inquire(axis_max=axis_max)
      endif

      call pg%inquire(rank=rank)

      ! Finish

      return

   end subroutine inquire_photgrid

   !****

   subroutine get_axis_label_specgrid(specgrid, i, axis_label) bind(C)

      type(C_PTR), value    :: specgrid
      integer(C_INT), value :: i
      character(C_CHAR)     :: axis_label(*)

      type(specgrid_t), pointer         :: sg
      integer                           :: rank
      character(LABEL_LEN), allocatable :: axis_labels(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(specgrid, sg)

      ! Get the i'th label

      call sg%inquire(rank=rank)

      allocate(axis_labels(rank))

      call sg%inquire(axis_labels=axis_labels)

      if (i >= 1 .AND. i <= SIZE(axis_labels)) then
         call f_c_string(axis_labels(i), axis_label)
      else
         axis_label(1:1) = C_NULL_CHAR
      endif

      ! Finish

      return

   end subroutine get_axis_label_specgrid

   !****

   subroutine get_axis_label_photgrid(photgrid, i, axis_label) bind(C)

      type(C_PTR), value    :: photgrid
      integer(C_INT), value :: i
      character(C_CHAR)     :: axis_label(*)

      type(photgrid_t), pointer         :: pg
      integer                           :: rank
      character(LABEL_LEN), allocatable :: axis_labels(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(photgrid, pg)

      ! Get the i'th label

      call pg%inquire(rank=rank)

      allocate(axis_labels(rank))

      call pg%inquire(axis_labels=axis_labels)

      if (i >= 1 .AND. i <= SIZE(axis_labels)) then
         call f_c_string(axis_labels(i), axis_label)
      else
         axis_label(1:1) = C_NULL_CHAR
      endif

      ! Finish

      return

   end subroutine get_axis_label_photgrid

   !****

   #:for name, var, arg_var, arg_type in (('intensity', 'I', 'mu', 'real(C_DOUBLE)'), &
                                          ('E_moment', 'E', 'k', 'integer(C_INT)'), &
                                          ('D_moment', 'D', 'l', 'integer(C_INT)'))

      subroutine interp_${name}$_specgrid(specgrid, vx, ${arg_var}$, n, lam, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value    :: specgrid
         real(C_DOUBLE)        :: vx(*)
         ${arg_type}$, value   :: ${arg_var}$
         integer(C_INT), value :: n
         real(C_DOUBLE)        :: lam(n)
         real(C_DOUBLE)        :: ${var}$(n-1)
         integer(C_INT)        :: stat
         logical(C_BOOL)       :: vderiv(*)

         type(specgrid_t), pointer :: sg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(specgrid, sg)

         ! Interpolate the ${name}$

         call sg%inquire(rank=rank)

         call sg%interp_${name}$(vx(1:rank), ${arg_var}$, lam, ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine interp_${name}$_specgrid

      !****

      subroutine interp_${name}$_photgrid(photgrid, vx, ${arg_var}$, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value  :: photgrid
         real(C_DOUBLE)      :: vx(*)
         ${arg_type}$, value :: ${arg_var}$
         real(C_DOUBLE)      :: ${var}$
         integer(C_INT)      :: stat
         logical(C_BOOL)     :: vderiv(*)

         type(photgrid_t), pointer :: pg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(photgrid, pg)

         ! Interpolate the ${name}$

         call pg%inquire(rank=rank)

         call pg%interp_${name}$(vx(1:rank), ${arg_var}$, ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine interp_${name}$_photgrid

   #:endfor

   !****
      
   #:for name, var in (('flux', 'F'),)

      subroutine interp_${name}$_specgrid(specgrid, vx, n, lam, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value    :: specgrid
         real(C_DOUBLE)        :: vx(*)
         integer(C_INT), value :: n
         real(C_DOUBLE)        :: lam(n)
         real(C_DOUBLE)        :: ${var}$(n-1)
         integer(C_INT)        :: stat
         logical(C_BOOL)       :: vderiv(*)

         type(specgrid_t), pointer :: sg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(specgrid, sg)

         ! Interpolate the ${name}$

         call sg%inquire(rank=rank)

         call sg%interp_${name}$(vx(1:rank), lam, ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine interp_${name}$_specgrid

      !****

      subroutine interp_${name}$_photgrid(photgrid, vx, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value :: photgrid
         real(C_DOUBLE)     :: vx(*)
         real(C_DOUBLE)     :: ${var}$
         integer(C_INT)     :: stat
         logical(C_BOOL)    :: vderiv(*)

         type(photgrid_t), pointer :: pg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(photgrid, pg)

         ! Interpolate the ${name}$

         call pg%inquire(rank=rank)

         call pg%interp_${name}$(vx(1:rank), ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine interp_${name}$_photgrid

   #:endfor

   !****

   subroutine open_file_(c_filename, hi, stat)

      character(C_CHAR)              :: c_filename(*)
      type(hdf5io_t), intent(out)    :: hi
      integer, intent(out), optional :: stat

      character(:), allocatable :: filename
      
      ! Open the file

      call c_f_string(c_filename, filename)

      if (is_hdf5(filename)) then

         hi = hdf5io_t(filename, OPEN_FILE_RO)

         if (PRESENT(stat)) stat = STAT_OK

      else

         if (PRESENT(stat)) then
            stat = STAT_FILE_NOT_FOUND
         else
            @:ABORT('file not found')
         endif

      endif

      ! Finish

      return

   end subroutine open_file_

end module cmsg_m
