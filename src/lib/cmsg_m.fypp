! Module  : cmsg_m
! Purpose : C public interface for MSG modules
!
! Copyright 2021 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module cmsg_m

   ! Uses

   use forum_m

   use msg_m

   use ISO_FORTRAN_ENV
   use ISO_C_BINDING

   ! No implicit typing

   implicit none

   ! Access specifiers

   private

   #:for prefix in ('specgrid', 'photgrid')
      public :: ${prefix}$_load
      public :: ${prefix}$_unload
      public :: ${prefix}$_inquire
      public :: ${prefix}$_interp_intensity
      public :: ${prefix}$_interp_D_moment
      public :: ${prefix}$_interp_flux
  #:endfor

   public :: specgrid_load_rebin

   ! Procedures

contains

   #:for prefix, type in (('specgrid', 'specgrid_t'), ('photgrid', 'photgrid_t'))

      function ${prefix}$_load(c_file_name) result(ptr) bind(C)

         character(C_CHAR) :: c_file_name(*)
         type(C_PTR)       :: ptr

         type(${type}$), pointer   :: gr
         character(:), allocatable :: file_name

         ! Allocate the grid

         allocate(gr)

         ! Load it from the file

         call c_f_string(c_file_name, file_name)

         gr = ${type}$(file_name)

         ! Set up the C pointer

         ptr = C_LOC(gr)

         ! Finish

         return

      end function ${prefix}$_load

      !****

      subroutine ${prefix}$_unload(ptr) bind(C)

         type(C_PTR), value :: ptr

         type(${type}$), pointer :: gr

         ! Set up the Fortran pointer

         call C_F_POINTER(ptr, gr)

         ! Deallocate the grid

         if (ASSOCIATED(gr)) deallocate(gr)

         ! Finish

         return

      end subroutine ${prefix}$_unload

   #:endfor

   !****

   function specgrid_load_rebin(c_file_name, w_0, dw, n_w) result(ptr) bind(C)

      character(C_CHAR)     :: c_file_name(*)
      real(C_DOUBLE), value :: w_0
      real(C_DOUBLE), value :: dw
      integer(C_INT), value :: n_w
      type(C_PTR)           :: ptr

      type(specgrid_t), pointer :: gr
      character(:), allocatable :: file_name
 
      ! Allocate the grid

      allocate(gr)

      ! Load it from the file

      call c_f_string(c_file_name, file_name)

      gr = specgrid_t(file_name, w_0, dw, n_w)

      ! Set up the C pointer

      ptr = C_LOC(gr)

      ! Finish

      return

   end function specgrid_load_rebin

   !****

   subroutine specgrid_inquire(ptr, w_0, dw, n_w, shape_ptr, rank, axis_minima_ptr, axis_maxima_ptr) bind(C)

      type(C_PTR), value :: ptr
      real(C_DOUBLE)     :: w_0
      real(C_DOUBLE)     :: dw
      integer(C_INT)     :: n_w
      type(C_PTR), value :: shape_ptr
      integer(C_INT)     :: rank
      type(C_PTR), value :: axis_minima_ptr
      type(C_PTR), value :: axis_maxima_ptr

      type(specgrid_t), pointer :: sg
      integer                   :: rank_
      integer, pointer          :: shape(:)
      real(RD), pointer         :: axis_minima(:)
      real(RD), pointer         :: axis_maxima(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(ptr, sg)

      ! Inquire metadata

      if (C_ASSOCIATED(shape_ptr)) then
         call sg%inquire(rank=rank_)
         call C_F_POINTER(shape_ptr, shape, [rank_])
         call sg%inquire(shape=shape)
      endif

      if (C_ASSOCIATED(axis_minima_ptr)) then
         call sg%inquire(rank=rank_)
         call C_F_POINTER(axis_minima_ptr, axis_minima, [rank_])
         call sg%inquire(axis_minima=axis_minima)
      endif

      if (C_ASSOCIATED(axis_maxima_ptr)) then
         call sg%inquire(rank=rank_)
         call C_F_POINTER(axis_maxima_ptr, axis_maxima, [rank_])
         call sg%inquire(axis_maxima=axis_maxima)
      endif

      call sg%inquire(w_0=w_0, dw=dw, n_w=n_w, rank=rank)

      ! Finish

      return

   end subroutine specgrid_inquire

   !****

   subroutine photgrid_inquire(ptr, shape_ptr, rank, axis_minima_ptr, axis_maxima_ptr) bind(C)

      type(C_PTR), value :: ptr
      type(C_PTR), value :: shape_ptr
      integer(C_INT)     :: rank
      type(C_PTR), value :: axis_minima_ptr
      type(C_PTR), value :: axis_maxima_ptr
      
      type(photgrid_t), pointer :: pg
      integer                   :: rank_
      integer, pointer          :: shape(:)
      real(RD), pointer         :: axis_minima(:)
      real(RD), pointer         :: axis_maxima(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(ptr, pg)

      ! Inquire metadata

      if (C_ASSOCIATED(shape_ptr)) then
         call pg%inquire(rank=rank_)
         call C_F_POINTER(shape_ptr, shape, [rank_])
         call pg%inquire(shape=shape)
      endif

      if (C_ASSOCIATED(axis_minima_ptr)) then
         call pg%inquire(rank=rank_)
         call C_F_POINTER(axis_minima_ptr, axis_minima, [rank_])
         call pg%inquire(axis_minima=axis_minima)
      endif

      if (C_ASSOCIATED(axis_maxima_ptr)) then
         call pg%inquire(rank=rank_)
         call C_F_POINTER(axis_maxima_ptr, axis_maxima, [rank_])
         call pg%inquire(axis_maxima=axis_maxima)
      endif

      call pg%inquire(rank=rank)

      ! Finish

      return

   end subroutine photgrid_inquire

   !****

   subroutine specgrid_get_axis_label(ptr, i, axis_label) bind(C)

      type(C_PTR), value    :: ptr
      integer(C_INT), value :: i
      character(C_CHAR)     :: axis_label(*)

      type(specgrid_t), pointer         :: sg
      integer                           :: rank
      character(LABEL_LEN), allocatable :: axis_labels(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(ptr, sg)

      ! Get the i'th label

      call sg%inquire(rank=rank)

      allocate(axis_labels(rank))

      call sg%inquire(axis_labels=axis_labels)

      if (i >= 1 .AND. i <= SIZE(axis_labels)) then
         call f_c_string(axis_labels(i), axis_label)
      else
         axis_label(1:1) = C_NULL_CHAR
      endif

      ! Finish

      return

   end subroutine specgrid_get_axis_label

   !****

   subroutine photgrid_get_axis_label(ptr, i, axis_label) bind(C)

      type(C_PTR), value    :: ptr
      integer(C_INT), value :: i
      character(C_CHAR)     :: axis_label(*)

      type(photgrid_t), pointer         :: pg
      integer                           :: rank
      character(LABEL_LEN), allocatable :: axis_labels(:)

      ! Set up the Fortran pointer

      call C_F_POINTER(ptr, pg)

      ! Get the i'th label

      call pg%inquire(rank=rank)

      allocate(axis_labels(rank))

      call pg%inquire(axis_labels=axis_labels)

      if (i >= 1 .AND. i <= SIZE(axis_labels)) then
         call f_c_string(axis_labels(i), axis_label)
      else
         axis_label(1:1) = C_NULL_CHAR
      endif

      ! Finish

      return

   end subroutine photgrid_get_axis_label

   !****

   #:for name, var, arg_var, arg_type in (('intensity', 'I', 'mu', 'real(C_DOUBLE)'), &
                                          ('D_moment', 'D', 'l', 'integer(C_INT)'))

      subroutine specgrid_interp_${name}$(ptr, vx, ${arg_var}$, w_0, n_w, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value    :: ptr
         real(C_DOUBLE)        :: vx(*)
         ${arg_type}$, value   :: ${arg_var}$
         real(C_DOUBLE), value :: w_0
         integer(C_INT), value :: n_w
         real(C_DOUBLE)        :: ${var}$(n_w)
         integer(C_INT)        :: stat
         logical(C_BOOL)       :: vderiv(*)

         type(specgrid_t), pointer :: sg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(ptr, sg)

         ! Interpolate the ${name}$

         call sg%inquire(rank=rank)

         call sg%interp_${name}$(vx(1:rank), ${arg_var}$, w_0, ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine specgrid_interp_${name}$

      !****

      subroutine photgrid_interp_${name}$(ptr, vx, ${arg_var}$, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value  :: ptr
         real(C_DOUBLE)      :: vx(*)
         ${arg_type}$, value :: ${arg_var}$
         real(C_DOUBLE)      :: ${var}$
         integer(C_INT)      :: stat
         logical(C_BOOL)     :: vderiv(*)

         type(photgrid_t), pointer :: pg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(ptr, pg)

         ! Interpolate the ${name}$

         call pg%inquire(rank=rank)

         call pg%interp_${name}$(vx(1:rank), ${arg_var}$, ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine photgrid_interp_${name}$

   #:endfor

   !****
      
   #:for name, var in (('flux', 'F'),)

      subroutine specgrid_interp_${name}$(ptr, vx, w_0, n_w, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value    :: ptr
         real(C_DOUBLE)        :: vx(*)
         real(C_DOUBLE), value :: w_0
         integer(C_INT), value :: n_w
         real(C_DOUBLE)        :: ${var}$(n_w)
         integer(C_INT)        :: stat
         logical(C_BOOL)       :: vderiv(*)

         type(specgrid_t), pointer :: sg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(ptr, sg)

         ! Interpolate the ${name}$

         call sg%inquire(rank=rank)

         call sg%interp_${name}$(vx(1:rank), w_0, ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine specgrid_interp_${name}$

      !****

      subroutine photgrid_interp_${name}$(ptr, vx, ${var}$, stat, vderiv) bind(C)

         type(C_PTR), value :: ptr
         real(C_DOUBLE)     :: vx(*)
         real(C_DOUBLE)     :: ${var}$
         integer(C_INT)     :: stat
         logical(C_BOOL)    :: vderiv(*)

         type(photgrid_t), pointer :: pg
         integer                   :: rank

         ! Set up the Fortran pointer

         call C_F_POINTER(ptr, pg)

         ! Interpolate the ${name}$

         call pg%inquire(rank=rank)

         call pg%interp_${name}$(vx(1:rank), ${var}$, stat, LOGICAL(vderiv(1:rank)))

         ! Finish

         return

      end subroutine photgrid_interp_${name}$

   #:endfor

end module cmsg_m
