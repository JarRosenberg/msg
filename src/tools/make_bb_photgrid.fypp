! Module   : make_bb_photgrid
! Purpose  : create a blackbody/bolometric photgrid file
!
! Copyright 2021 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

program make_bb_photgrid

   ! Uses

   use forum_m, PI_ => PI
   use msg_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Variables

   real(RD)                  :: Teff_min
   real(RD)                  :: Teff_max
   integer                   :: n_Teff
   real(RD)                  :: logg_min
   real(RD)                  :: logg_max
   integer                   :: n_logg
   integer                   :: n_mu
   real(RD)                  :: u
   character(:), allocatable :: law_str
   character(:), allocatable :: photgrid_file_name

   type(axis_t)                      :: axes(2)
   integer                           :: n_nodes
   logical, allocatable              :: mask(:)
   type(vgrid_t)                     :: vgrid
   type(limb_t)                      :: limb
   integer                           :: i
   real(RD), allocatable             :: mu(:)
   real(RD), allocatable             :: f_mu(:)
   type(limb_photint_t), allocatable :: photints(:)
   type(mem_photsource_t)            :: photsource
   type(photgrid_t)                  :: photgrid
   type(hdf5io_t)                    :: hdf5io

   ! Read command-line arguments

   @:ASSERT(n_arg() == 10, 'Syntax: make_bb_photgrid Teff_min Teff_max n_Teff logg_min logg_max n_logg n_mu u law_str photgrid_file_name')

   call get_arg(1, Teff_min)
   call get_arg(2, Teff_max)
   call get_arg(3, n_Teff)

   call get_arg(4, logg_min)
   call get_arg(5, logg_max)
   call get_arg(6, n_logg)

   call get_arg(7, n_mu)
   call get_arg(8, u)
   call get_arg(9, law_str)

   call get_arg(10, photgrid_file_name)

   ! Create the axes

   axes(1) = make_axis(LOG10(Teff_min), LOG10(Teff_max), n_Teff, 'logT')
   axes(2) = make_axis(logg_min, logg_max, n_logg, 'logg')

   ! Create vgrid

   n_nodes = n_Teff*n_logg

   allocate(mask(n_nodes))
   mask = .TRUE.

   vgrid = vgrid_t(axes, mask)

   ! Set up limb-darkening stuff

   limb = limb_t(law_str)

   mu = [(REAL(i-1, RD)/(n_mu-1),i=1,n_mu)]
   f_mu = (1._RD - u*(1._RD - mu))

   ! Create photints

   allocate(photints(n_nodes))

   call vgrid%map(make_photint)

   ! Create photsource

   photsource = mem_photsource_t(photints)

   ! Create photgrid

   photgrid = photgrid_t(photsource, vgrid)

   ! Write it

   hdf5io = hdf5io_t(photgrid_file_name, CREATE_FILE)
   call photgrid%write(hdf5io)
   call hdf5io%final()

   ! Finish

contains

   function make_axis (x_min, x_max, n, label) result(axis)

      real(RD), intent(in)     :: x_min
      real(RD), intent(in)     :: x_max
      integer, intent(in)      :: n
      character(*), intent(in) :: label
      type(axis_t)             :: axis

      integer  :: i
      real(RD) :: x(n)

      ! Set up axis values

      x = [((x_min*(n-i) + x_max*(i-1))/(n-1),i=1,n)]

      ! Create the axis

      axis = axis_t(x, label)

      ! Finish

      return

   end function make_axis

   !****

   subroutine make_photint (i_vec, stat)

      integer, intent(in)            :: i_vec(:)
      integer, intent(out), optional :: stat

      integer  :: i_lin
      real(RD) :: logT
      real(RD) :: Teff
      real(RD) :: I(n_mu)

      ! Convert vector indices into linear indices

      i_lin = vgrid%lin_from_vec(i_vec)

      ! Create intensity data

      call axes(1)%fetch(i_vec(1), logT)
      Teff = 10**logT

      I = SIGMA_STEFAN*Teff**4/(TWOPI*(1._RD/2._RD - u/6._RD)) * f_mu

      ! Construct the photint_t

      photints(i_lin) = limb_photint_t(I, mu, limb)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine make_photint

end program make_bb_photgrid
