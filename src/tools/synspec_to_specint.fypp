! Module   : synspec_to_specint
! Purpose  : create specint files from synspec files
!
! Copyright 2021 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

program synspec_to_specint

   ! Uses

   use kinds_m
   use hdf5io_m
   use memory_m
   use order_m
   use system_m

   use const_m
   use limb_m
   use libmsg_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Variables

   character(:), allocatable :: synspec_filename
   integer                   :: n_mu
   real(RD)                  :: mu_0
   real(RD)                  :: R
   real(RD)                  :: lambda_min
   real(RD)                  :: lambda_max
   character(:), allocatable :: limb_law
   character(:), allocatable :: specint_filename

   type(limb_t)              :: li
   real(RD), allocatable     :: I(:,:)
   real(RD), allocatable     :: lambda(:)
   integer                   :: n_lambda
   real(RD)                  :: w_0
   real(RD)                  :: dw
   integer                   :: n_w
   integer                   :: j
   real(RD), allocatable     :: mu(:)
   type(specint_t)           :: si
   type(hdf5io_t)            :: hi
   type(hdf5io_t)            :: hi_la
   character(:), allocatable :: label
   real(RD)                  :: value

   ! Read command-line arguments

   @:ASSERT(n_arg() >= 8 .AND. MOD(n_arg(), 2)==0, {Syntax: synspec_to_specint synspec_filename n_mu mu_0 lambda_min lambda_max R limb_law specint_filename [label value] [label value]...})

   call get_arg(1, synspec_filename)
   call get_arg(2, n_mu)
   call get_arg(3, mu_0)
   call get_arg(4, lambda_min)
   call get_arg(5, lambda_max)
   call get_arg(6, R)
   call get_arg(7, limb_law)
   call get_arg(8, specint_filename)

   ! Set up the limb-darkening law

   li = limb_t(limb_law)

   ! Read data from the synspec file

   call read_data(synspec_filename, n_mu, lambda, I)

   n_lambda = SIZE(lambda)

   ! Set up binning parameters

   dw = 1._RD/R
   w_0 = LOG(lambda_min)
   n_w = FLOOR((LOG(lambda_max) - LOG(lambda_min))/dw)

   ! Create the specint_t

   mu = [((mu_0*(n_mu-j) + 1._RD*(j-1))/(n_mu-1), j=1,n_mu)]

   si = specint_t(I, mu, lambda, w_0, dw, n_w, li)

   ! Write the specint_t (together with labels)

   hi = hdf5io_t(specint_filename, CREATE_FILE, comp_level=9)

   call write(hi, si)

   hi_la = hdf5io_t(hi, 'labels')

   do j = 9, n_arg(), 2
      call get_arg(j, label)
      call get_arg(j+1, value)
      call hi_la%write_dset(label, value)
   end do

   call hi_la%final()

   call hi%final()

   ! Finish

contains

   subroutine read_data (filename, n_mu, lambda, I)

      character(*), intent(in)           :: filename
      integer, intent(in)                :: n_mu
      real(RD), allocatable, intent(out) :: lambda(:)
      real(RD), allocatable, intent(out) :: I(:,:)

      integer              :: unit
      real(RD)             :: H
      integer              :: d_lam
      integer              :: n_lam
      integer, allocatable :: ind(:)
      integer              :: i_lam
      integer              :: n_dup

      ! Open the input file

      open(NEWUNIT=unit, FILE=filename, STATUS='OLD')

      ! Read in the wavelength and intensity data

      d_lam = 1024

      allocate(lambda(d_lam))
      allocate(I(n_mu,d_lam))

      n_lam = 0

      read_loop : do

         n_lam = n_lam + 1

         ! If necessary, expand arrays

         if(n_lam > d_lam) then
            d_lam = d_lam*2
            call reallocate(lambda, [d_lam])
            call reallocate(I, [n_mu,d_lam])
         endif

         ! Read a block of data

         read(unit, *, END=100) lambda(n_lam), H
         read(unit,*) I(:,n_lam)

         I(:,n_lam) = I(n_mu:1:-1,n_lam)

         ! Convert from to ergs/s/cm**2/Hz/sr to ergs/s/cm**2/Angstrom/sr

         I(:,n_lam) = I(:,n_lam)*C_LIGHT/(lambda(n_lam)*1E-8_RD)**2*1E-8_RD

      end do read_loop

100   continue

      close(unit)

      n_lam = n_lam - 1
      d_lam = n_lam

      call reallocate(lambda, [d_lam])
      call reallocate(I, [n_mu,d_lam])

      write(OUTPUT_UNIT, *) 'n_lam (as read) :', n_lam

      ! Sort the data by wavelength

      ind = sort_indices(lambda)

      lambda = lambda(ind)
      I = I(:,ind)

      ! Merge duplicate points

      n_lam = 1

      n_dup = 0

      merge_loop : do i_lam = 2,d_lam

         if(lambda(i_lam) == lambda(n_lam)) then

            I(:,n_lam) = I(:,n_lam) + I(:,i_lam)
            n_dup = n_dup + 1

         else

            if(n_dup > 0) then

               I(:,n_lam) = I(:,n_lam)/(n_dup + 1)
               n_dup = 0

            endif

            n_lam = n_lam + 1

            lambda(n_lam) = lambda(i_lam)
            I(:,n_lam) = I(:,i_lam)

         endif

      end do merge_loop

      if(n_dup > 0) then
         I(:,n_lam) = I(:,n_lam)/(n_dup + 1)
      endif

      d_lam = n_lam

      call reallocate(lambda, [d_lam])
      call reallocate(I, [n_mu,d_lam])

      write(OUTPUT_UNIT, *) 'n_lam (merged)  :', n_lam

      ! Finish

      return

   end subroutine read_data

end program synspec_to_specint
