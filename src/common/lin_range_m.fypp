! Module  : lin_range_m
! Purpose : define lin_range_t type, representing a linearly distributed
!           range
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module lin_range_m

   ! Uses

   use kinds_m
   use hdf5io_m
   use order_m, locate_ => locate
   use stat_m

   use range_m, only: range_t
   use stat_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(range_t) :: lin_range_t
      private
      real(RD) :: x_0
      real(RD) :: dx
      integer  :: n
   contains
      private
      procedure, public :: inquire
      procedure, public :: get
      procedure, public :: get_all
      procedure, public :: locate
   end type lin_range_t

   ! Interfaces

   interface lin_range_t
      module procedure lin_range_t_
      module procedure lin_range_t_subset_
   end interface lin_range_t

   interface read
      module procedure read_
   end interface read

   interface write
      module procedure write_
   end interface write

   ! Access specifiers

   private

   public :: lin_range_t
   public :: read
   public :: write

contains

   function lin_range_t_(x_0, dx, n) result (ra)

      real(RD), intent(in) :: x_0
      real(RD), intent(in) :: dx
      integer, intent(in)  :: n
      type(lin_range_t)    :: ra

      ! Construct the lin_range_t from the supplied data

      @:ASSERT(n >= 2,'insufficient data')

      ra%x_0 = x_0
      ra%dx = dx

      ra%n = n

      ! Finish

      return

   end function lin_range_t_

   !****

   function lin_range_t_subset_(ra_in, i_a, i_b) result(ra)

     type(lin_range_t), intent(in) :: ra_in
     integer, intent(in)           :: i_a
     integer, intent(in)           :: i_b
     type(lin_range_t)             :: ra

     ! Construct the lin_range_t by subsetting ra_in

     ra = lin_range_t(ra_in%x_0+ra_in%dx*(i_a-1), ra_in%dx, i_b-i_a+1)

     ! Finish

     return

   end function lin_range_t_subset_

   !****

   subroutine inquire(self, n, min, max)

      class(lin_range_t), intent(in)  :: self
      integer, intent(out), optional  :: n
      real(RD), intent(out), optional :: min
      real(RD), intent(out), optional :: max

      ! Inquire metadata

      if (PRESENT(n)) n = self%n

      if (PRESENT(min)) min = self%x_0
      if (PRESENT(max)) max = self%x_0 + self%dx*(self%n-1)

      ! Finish

      return

   end subroutine inquire

   !****

   subroutine get(self, i, x, stat)

      class(lin_range_t), intent(in) :: self
      integer, intent(in)            :: i
      real(RD), intent(out)          :: x
      integer, intent(out), optional :: stat

      ! Get the i'th value

      if (i < 1) then
         if (PRESENT(stat)) then
            stat = STAT_OUT_OF_BOUNDS_RANGE_LO
            return
         else
            @:ABORT('out-of-bounds (lo) in range')
         endif
      endif

      if (i > self%n) then
         if (PRESENT(stat)) then
            stat = STAT_OUT_OF_BOUNDS_RANGE_HI
            return
         else
            @:ABORT('out-of-bounds (hi) in range')
         endif
      endif

      x = self%x_0 + self%dx*(i-1)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine get

   !****

   subroutine get_all(self, x)

      class(lin_range_t), intent(in) :: self
      real(RD), intent(out)          :: x(:)

      integer :: i

      @:CHECK_BOUNDS(SIZE(x), self%n)

      ! Get all values

      do i = 1, self%n
         x(i) = self%x_0 + self%dx*(i-1)
      end do

      ! Finish

      return

   end subroutine get_all

   !****

   subroutine locate(self, x, i, right)

      class(lin_range_t), intent(in) :: self
      real(RD), intent(in)           :: x
      integer, intent(inout)         :: i
      logical, intent(in), optional  :: right

      ! Locate where in the range x falls

      call locate_(self%x_0, self%dx, x, i, right=right)

      i = MIN(MAX(i, 0), self%n+1)

      ! Finish

      return

   end subroutine locate

   !****

   subroutine read_(hi, ra, skip_type)

      type(hdf5io_t), intent(inout)  :: hi
      type(lin_range_t), intent(out) :: ra
      logical, intent(in), optional  :: skip_type

      logical             :: skip_type_
      character(TYPE_LEN) :: type
      real(RD)            :: x_0
      real(RD)            :: dx
      integer             :: n

      if (PRESENT(skip_type)) then
         skip_type_ = skip_type
      else
         skip_type_ = .FALSE.
      endif

      ! Read the lin_range_t

      if (.NOT. skip_type_) then
         call hi%read_attr('TYPE', type)
         @:ASSERT(type == 'lin_range_t', 'invalid type')
      endif

      call hi%read_dset('x_0', x_0)
      call hi%read_dset('dx', dx)
      call hi%read_dset('n', n)
  
      ra = lin_range_t(x_0, dx, n)

      ! Finish

      return

   end subroutine read_

   !****

   subroutine write_(hi, ra, skip_type)

      type(hdf5io_t), intent(inout)  :: hi
      class(lin_range_t), intent(in) :: ra
      logical, intent(in), optional  :: skip_type

      logical :: skip_type_

      if (PRESENT(skip_type)) then
         skip_type_ = skip_type
      else
         skip_type_ = .FALSE.
      endif

      ! Write the lin_range_t

      if (.NOT. skip_type_) then
         call hi%write_attr('TYPE', 'lin_range_t')
      end if
      
      call hi%write_dset('x_0', ra%x_0)
      call hi%write_dset('dx', ra%dx)
      call hi%write_dset('n', ra%n)
     
      ! Finish

      return

   end subroutine write_

end module lin_range_m
