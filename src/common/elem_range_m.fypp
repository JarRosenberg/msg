! Module  : range_elem_m
! Purpose : define elem_range_t type, which wraps a range_t to allow
!           storage as an array element
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module elem_range_m

   ! Uses

   use forum_m

   use range_m
   use stat_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(range_t) :: elem_range_t
      private
      class(range_t), allocatable :: range
   contains
      private
      procedure, public :: inquire
      procedure, public :: expand
      procedure, public :: subset
      procedure, public :: read
      procedure, public :: write
   end type elem_range_t

   ! Interfaces

   interface elem_range_t
      module procedure elem_range_t_
   end interface elem_range_t

   ! Access specifiers

   private

   public :: elem_range_t

contains

   function elem_range_t_(range_in) result(range)

      class(range_t), intent(in) :: range_in
      type(elem_range_t)         :: range

      ! Construct range from range_in

      range%range = range_in

      ! Finish

      return

   end function elem_range_t_

   !****

   subroutine subset(self, i_a, i_b, range)

      class(elem_range_t), intent(in)          :: self
      integer, intent(in)                      :: i_a
      integer, intent(in)                      :: i_b
      class(range_t), allocatable, intent(out) :: range

      ! Pass

      call self%range%subset(i_a, i_b, range)

      ! Finish

      return

   end subroutine subset

   !****

   subroutine inquire(self, n, min, max)

      class(elem_range_t), intent(in)  :: self
      integer, intent(out), optional   :: n
      real(RD), intent(out), optional  :: min
      real(RD), intent(out), optional  :: max

      ! Pass

      call self%range%inquire(n, min, max)

      ! Finish

      return

   end subroutine inquire

   !****

   subroutine expand(self, x)

      class(elem_range_t), intent(in) :: self
      real(RD), intent(out)           :: x(:)

      ! Pass

      call self%range%expand(x)
      
      ! Finish

      return

   end subroutine expand

   !****

   subroutine read(self, hdf5io, stat)

      class(elem_range_t), intent(out) :: self
      type(hdf5io_t), intent(inout)    :: hdf5io
      integer, intent(out), optional   :: stat

      character(TYPE_LEN)         :: type
      type(hdf5io_t)              :: hdf5io_range
      character(TYPE_LEN)         :: type_range
      class(range_t), allocatable :: range

      ! Read the range

      call hdf5io%read_attr('TYPE', type)
      if (type /= 'elem_range_t') then
         if (PRESENT(stat)) then
            stat = STAT_INVALID_TYPE
            return
         else
            @:ABORT('invalid type')
         end if
      end if

      hdf5io_range = hdf5io_t(hdf5io, 'range')
      call hdf5io_range%read_attr('TYPE', type_range)
      range = range_t(type_range)
      call range%read(hdf5io_range, stat)
      call hdf5io_range%final()
      if (PRESENT(stat)) then
         if (stat /= STAT_OK) return
      end if

      select type(self)
      type is (elem_range_t)
         self = elem_range_t(range)
      class default
         @:ABORT('invalid type')
      end select

      ! Finish

      return

   end subroutine read

   !****

   subroutine write(self, hdf5io, stat)
 
      class(elem_range_t), intent(in) :: self
      type(hdf5io_t), intent(inout)   :: hdf5io
      integer, intent(out), optional  :: stat

      type(hdf5io_t) :: hdf5io_range

      ! Write the range

      call hdf5io%write_attr('TYPE', 'elem_range_t')

      hdf5io_range = hdf5io_t(hdf5io, 'range')
      call self%range%write(hdf5io_range, stat)
      call hdf5io_range%final()
      if (PRESENT(stat)) then
         if (stat /= STAT_OK) return
      end if
     
      ! Finish

      return

   end subroutine write

end module elem_range_m
