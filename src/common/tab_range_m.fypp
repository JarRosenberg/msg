! Module  : tab_range_m
! Purpose : define tab_range_t type, representing a tabulated
!           range
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module tab_range_m

   ! Uses

   use kinds_m
   use hdf5io_m
   use order_m, locate_ => locate
   use stat_m

   use range_m, only: range_t
   use stat_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(range_t) :: tab_range_t
      private
      real(RD), allocatable :: x(:)
      integer               :: n
   contains
      private
      procedure, public :: inquire
      procedure, public :: locate
      procedure, public :: get
      procedure, public :: expand
   end type tab_range_t

   ! Interfaces

   interface tab_range_t
      module procedure tab_range_t_
   end interface tab_range_t

   interface read
      module procedure read_
   end interface read

   interface write
      module procedure write_
   end interface write

   ! Access specifiers

   private

   public :: tab_range_t
   public :: read
   public :: write

contains

   function tab_range_t_(x) result (ra)

      real(RD), intent(in) :: x(:)
      type(tab_range_t)    :: ra

      integer :: n

      ! Construct the tab_range_t from the supplied data

      n = SIZE(x)

      @:ASSERT(n >= 2,'insufficient data')
      @:ASSERT_DEBUG(ALL(x(2:) > x(:n-1)),'non-monotonic data')

      ra%x = x
      ra%n = n

      ! Finish

      return

   end function tab_range_t_

   !****

   subroutine inquire(self, n, min, max)

      class(tab_range_t), intent(in)  :: self
      integer, intent(out), optional  :: n
      real(RD), intent(out), optional :: min
      real(RD), intent(out), optional :: max

      ! Inquire metadata

      if (PRESENT(n)) n = self%n

      if (PRESENT(min)) min = self%x(1)
      if (PRESENT(max)) max = self%x(self%n)

      ! Finish

      return

   end subroutine inquire

   !****

   subroutine locate(self, x, i)

      class(tab_range_t), intent(in) :: self
      real(RD), intent(in)           :: x
      integer, intent(inout)         :: i

      ! Locate where in the range x falls

      call locate_(self%x, x, i)

      ! Finish

      return

   end subroutine locate

   !****

   subroutine get(self, i, x, stat)

      class(tab_range_t), intent(in) :: self
      integer, intent(in)            :: i
      real(RD), intent(out)          :: x
      integer, intent(out), optional :: stat

      ! Locate where in the range x falls

      if (i < 1) then
         if (PRESENT(stat)) then
            stat = STAT_OUT_OF_BOUNDS_RANGE_LO
            return
         else
            @:ABORT('out-of-bounds (lo) in range')
         endif
      endif

      if (i > self%n) then
         if (PRESENT(stat)) then
            stat = STAT_OUT_OF_BOUNDS_RANGE_HI
            return
         else
            @:ABORT('out-of-bounds (hi) in range')
         endif
      endif

      x = self%x(i)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine get

   !****

   subroutine expand(self, x)

      class(tab_range_t), intent(in) :: self
      real(RD), intent(out)          :: x(:)

      @:CHECK_BOUNDS(SIZE(x), self%n)

      ! Expand the range

      x = self%x

      ! Finish

      return

   end subroutine expand

   !****

   subroutine read_(hi, ra)

      type(hdf5io_t), intent(inout)  :: hi
      type(tab_range_t), intent(out) :: ra

      character(TYPE_LEN)   :: type
      real(RD), allocatable :: x(:)

      ! Read the tab_range_t

      call hi%read_attr('TYPE', type)
      @:ASSERT(type == 'tab_range_t', 'invalid type')

      call hi%alloc_read_dset('x', x)
  
      ra = tab_range_t(x)

      ! Finish

      return

   end subroutine read_

   !****

   subroutine write_(hi, ra)

      type(hdf5io_t), intent(inout)  :: hi
      class(tab_range_t), intent(in) :: ra

      ! Write the tab_range_t

      call hi%write_attr('TYPE', 'tab_range_t')
      
      call hi%write_dset('x', ra%x)

      ! Finish

      return

   end subroutine write_

end module tab_range_m
