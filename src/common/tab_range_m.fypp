! Module  : tab_range_m
! Purpose : define tab_range_t type, representing a tabulated
!           range
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module tab_range_m

   ! Uses

   use forum_m

   use range_m
   use stat_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(range_t) :: tab_range_t
      private
      real(RD), allocatable :: x(:)
      integer               :: n
   contains
      private
      procedure, public :: inquire
      procedure, public :: expand
      procedure, public :: subset
      procedure, public :: read
      procedure, public :: write
   end type tab_range_t

   ! Interfaces

   interface tab_range_t
      module procedure tab_range_t_
   end interface tab_range_t

   ! Access specifiers

   private

   public :: tab_range_t

contains

   function tab_range_t_(x) result (range)

      real(RD), intent(in) :: x(:)
      type(tab_range_t)    :: range

      integer :: n

      ! Construct range from the supplied data

      n = SIZE(x)

      @:ASSERT(n >= 1,'insufficient data')
      @:ASSERT_DEBUG(ALL(x(2:) > x(:n-1)),'non-monotonic data')

      range%x = x
      range%n = n

      ! Finish

      return

   end function tab_range_t_

   !****

   subroutine inquire(self, n, min, max)

      class(tab_range_t), intent(in)  :: self
      integer, intent(out), optional  :: n
      real(RD), intent(out), optional :: min
      real(RD), intent(out), optional :: max

      ! Inquire metadata

      if (PRESENT(n)) n = self%n

      if (PRESENT(min)) min = self%x(1)
      if (PRESENT(max)) max = self%x(self%n)

      ! Finish

      return

   end subroutine inquire

   !****

   subroutine expand(self, x)

      class(tab_range_t), intent(in) :: self
      real(RD), intent(out)          :: x(:)

      @:CHECK_BOUNDS(SIZE(x), self%n)

      ! Expand the range into the array

      x = self%x

      ! Finish

      return

   end subroutine expand

   !****
    
   subroutine subset(self, i_a, i_b, range)

      class(tab_range_t), intent(in)           :: self
      integer, intent(in)                      :: i_a
      integer, intent(in)                      :: i_b
      class(range_t), allocatable, intent(out) :: range

      ! Subset the range

      @:ASSERT(i_a >= 1, 'invalid argument')
      @:ASSERT(i_b <= self%n, 'invalid argument')
      @:ASSERT(i_b >= i_a, 'invalid argument')

      range = tab_range_t(self%x(i_a:i_b))

      ! Finish

      return

   end subroutine subset

   !****

   subroutine read(self, hdf5io, stat)

      class(tab_range_t), intent(out) :: self
      type(hdf5io_t), intent(inout)   :: hdf5io
      integer, intent(out), optional :: stat

      character(TYPE_LEN)   :: type
      real(RD), allocatable :: x(:)

      ! Read the range

      call hdf5io%read_attr('TYPE', type)
      if (type /= 'tab_range_t') then
         if (PRESENT(stat)) then
            stat = STAT_INVALID_TYPE
            return
         else
            @:ABORT('invalid type')
         endif
      endif

      call hdf5io%alloc_read_dset('x', x)

      select type(self)
      type is (tab_range_t)
         self = tab_range_t(x)
      class default
         @:ABORT('invalid type')
      end select

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine read

   !****

   subroutine write(self, hdf5io, stat)

      class(tab_range_t), intent(in) :: self
      type(hdf5io_t), intent(inout)  :: hdf5io
      integer, intent(out), optional :: stat

      ! Write range

      call hdf5io%write_attr('TYPE', 'tab_range_t')
      
      call hdf5io%write_dset('x', self%x)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine write

end module tab_range_m
