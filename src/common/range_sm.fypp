! Submodule : range_sm
! Purpose   : Factory & I/O routines for range_m
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

submodule (range_m) range_sm

   ! Uses

   use kinds_m
   use hdf5io_m

   use lin_range_m, read_lin => read, write_lin => write
   use log_range_m, read_log => read, write_log => write
   use tab_range_m, read_tab => read, write_tab => write

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

contains

   function range_t_subset_(range_in, i_a, i_b) result(range)

      class(range_t), intent(in)  :: range_in
      integer, intent(in)         :: i_a
      integer, intent(in)         :: i_b
      class(range_t), allocatable :: range

      ! Construct range by subsetting range_in

      select type(range_in)
      type is(lin_range_t)
         range = lin_range_t(range_in, i_a, i_b)
      type is (log_range_t)
         range = log_range_t(range_in, i_a, i_b)
      type is(tab_range_t)
         range = tab_range_t(range_in, i_a, i_b)
      class default
         @:ABORT('invalid type')
      end select

      ! Finish

      return

   end function range_t_subset_
         
   !****
   
   subroutine read_(hdf5io, range, stat)

      type(hdf5io_t), intent(inout)            :: hdf5io
      class(range_t), allocatable, intent(out) :: range
      integer, intent(out), optional           :: stat

      character(TYPE_LEN) :: type

      ! Read range

      call hdf5io%read_attr('TYPE', type)

      select case(type)
      case('lin_range_t')
         allocate(lin_range_t::range)
      case('log_range_t')
         allocate(log_range_t::range)
      case('tab_range_t')
         allocate(tab_range_t::range)
      case default
         @:ABORT('invalid type')
      end select

      select type(range)
      type is (lin_range_t)
         call read_lin(hdf5io, range, stat)
      type is (log_range_t)
         call read_log(hdf5io, range, stat)
      type is (tab_range_t)
         call read_tab(hdf5io, range, stat)
      class default
         @:ABORT('invalid type')
      end select

      ! Finish

      return

   end subroutine read_

   !****

   subroutine write_(hdf5io, range, stat)

      type(hdf5io_t), intent(inout)  :: hdf5io
      class(range_t), intent(in)     :: range
      integer, intent(out), optional :: stat

      ! Write range

      select type(range)
      type is (lin_range_t)
         call write_lin(hdf5io, range, stat)
      type is (log_range_t)
         call write_log(hdf5io, range, stat)
      type is (tab_range_t)
         call write_tab(hdf5io, range, stat)
      class default
         @:ABORT('invalid type')
      end select

      ! Finish

      return

   end subroutine write_

end submodule range_sm
