! Module  : radint_m
! Purpose : define radint_t type, for representing radiant intensity
!
! Copyright 2021 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module radint_m

   ! Uses

   use kinds_m
   use hdf5io_m

   use limb_m
   use math_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type radint_t
      private
      type(limb_t)          :: li
      real(RD)              :: I_1
      real(RD), allocatable :: a(:)
   contains
      private
      procedure             :: op_add_ri_ri_
      generic, public       :: operator(+) => op_add_ri_ri_
      procedure             :: op_sub_ri_ri_
      generic, public       :: operator(-) => op_sub_ri_ri_
      procedure             :: op_mul_ri_rd_
      procedure, pass(self) :: op_mul_rd_ri_
      generic, public       :: operator(*) => op_mul_ri_rd_, op_mul_rd_ri_
      procedure             :: op_div_ri_rd_
      generic, public       :: operator(/) => op_div_ri_rd_
      procedure, public     :: intensity
      procedure, public     :: D_moment
      procedure, public     :: flux
   end type radint_t

   ! Interfaces

   interface radint_t
      module procedure radint_t_
      module procedure radint_t_fit_
   end interface radint_t

   interface read
      module procedure read_
   end interface read

   interface write
      module procedure write_
   end interface write

   ! Access specifiers

   private

   public :: radint_t
   public :: read
   public :: write

   ! Procedures

contains

   function radint_t_(I_1, a, li) result (ri)

      real(RD), intent(in)     :: I_1
      real(RD), intent(in)     :: a(:)
      type(limb_t), intent(in) :: li
      type(radint_t)           :: ri

      integer :: n_b

      ! Construct the radint_t from the supplied data

      n_b = li%n_basis()

      @:CHECK_BOUNDS(SIZE(a), n_b)

      ri%li = li

      ri%I_1 = I_1
      ri%a = a

      ! Finish

      return

   end function radint_t_

   !****

   function radint_t_fit_(I, mu, li) result (ri)

      real(RD), intent(in)     :: I(:)
      real(RD), intent(in)     :: mu(:)
      type(limb_t), intent(in) :: li
      type(radint_t)           :: ri

      real(RD)              :: I_1
      real(RD), allocatable :: a(:)

      ! Construct the radint_t by fitting the supplied I(mu)
      ! intensity data

      call li%fit_intensity(I, mu, I_1, a)

      ri = radint_t(I_1, a, li)

      ! Finish

      return

   end function radint_t_fit_

   !****

   function op_add_ri_ri_ (self, other) result (ri)

      class(radint_t), intent(in) :: self
      type(radint_t), intent(in)  :: other
      type(radint_t)              :: ri

      real(RD)              :: I_1
      real(RD), allocatable :: a(:)

      ! Add the radint_t's

      @:ASSERT(self%li == other%li,mismatched limb-darkening laws)

      I_1 = self%I_1 + other%I_1
      a = (self%I_1*self%a + other%I_1*other%a)/I_1

      ri = radint_t(I_1, a, self%li)

      ! Finish

      return

   end function op_add_ri_ri_

   !****

   function op_sub_ri_ri_ (self, other) result (ri)

      class(radint_t), intent(in) :: self
      type(radint_t), intent(in)  :: other
      type(radint_t)              :: ri

      real(RD)              :: I_1
      real(RD), allocatable :: a(:)

      ! Subtract the radint_t's

      @:ASSERT(self%li == other%li,mismatched limb-darkening laws)

      I_1 = self%I_1 - other%I_1
      a = (self%I_1*self%a - other%I_1*other%a)/I_1

      ri = radint_t(I_1, a, self%li)

      ! Finish

      return

   end function op_sub_ri_ri_

   !****

   function op_mul_ri_rd_ (self, other) result (ri)

      class(radint_t), intent(in) :: self
      real(RD), intent(in)        :: other
      type(radint_t)              :: ri

      real(RD)              :: I_1
      real(RD), allocatable :: a(:)

      ! Multiply the radint_t

      I_1 = self%I_1*other
      a = self%a

      ri = radint_t(I_1, a, self%li)

      ! Finish

      return

   end function op_mul_ri_rd_

   !****

   function op_mul_rd_ri_ (other, self) result (ri)

      real(RD), intent(in)        :: other
      class(radint_t), intent(in) :: self
      type(radint_t)              :: ri

      real(RD)              :: I_1
      real(RD), allocatable :: a(:)

      ! Multiply the radint_t

      I_1 = other*self%I_1
      a = self%a

      ri = radint_t(I_1, a, self%li)

      ! Finish

      return

   end function op_mul_rd_ri_

   !****

   function op_div_ri_rd_ (self, other) result (ri)

      class(radint_t), intent(in) :: self
      real(RD), intent(in)        :: other
      type(radint_t)              :: ri

      real(RD)              :: I_1
      real(RD), allocatable :: a(:)

      ! Divide the radint_t

      I_1 = self%I_1/other
      a = self%a

      ri = radint_t(I_1, a, self%li)

      ! Finish

      return

   end function op_div_ri_rd_

   !****

   #:for var, var_name, arg_name, arg_type in (('intensity', 'I', 'mu', 'real(RD)'), &
                                               ('D_moment', 'D', 'l', 'integer'))

      function ${var}$(self, ${arg_name}$) result(${var_name}$)

         class(radint_t), intent(in) :: self
         ${arg_type}$, intent(in)    :: ${arg_name}$
         real(RD)                    :: ${var_name}$

         ! Evaluate ${var_name}$

         ${var_name}$ = self%li%${var}$(self%I_1, self%a, ${arg_name}$)

         ! Finish

         return

      end function ${var}$

   #:endfor
      
   !****

   #:for var, var_name in (('flux', 'F'), )

      function ${var}$(self) result(${var_name}$)

         class(radint_t), intent(in) :: self
         real(RD)                    :: ${var_name}$

         ! Evaluate ${var_name}$

         ${var_name}$ = self%li%${var}$(self%I_1, self%a)

         ! Finish

         return

      end function ${var}$

   #:endfor
      
   !****

   subroutine read_(hi, ri)

      type(hdf5io_t), intent(inout) :: hi
      type(radint_t), intent(out)   :: ri

      character(TYPE_LEN)   :: type
      type(hdf5io_t)        :: hi_li
      type(limb_t)          :: li
      real(RD), allocatable :: I_1
      real(RD), allocatable :: a(:)

      ! Read the radint_t

      call hi%read_attr('TYPE', type)
      @:ASSERT(type == 'radint_t',invalid type)

      hi_li = hdf5io_t(hi, 'li')
      call read(hi_li, li)
      call hi_li%final()

      call hi%alloc_read_dset('I_1', I_1)
      call hi%alloc_read_dset('a', a)

      ri = radint_t(I_1, a, li)

      ! Finish

      return

   end subroutine read_

   !****

   subroutine write_ (hi, ri)

      type(hdf5io_t), intent(inout) :: hi
      type(radint_t), intent(in)    :: ri

      type(hdf5io_t) :: hi_li

      ! Write the radint_t

      call hi%write_attr('TYPE', 'radint_t')

      hi_li = hdf5io_t(hi, 'li')
      call write(hi_li, ri%li)
      call hi_li%final()

      call hi%write_dset('I_1', ri%I_1)
      call hi%write_dset('a', ri%a)

      ! Finish

      return

   end subroutine write_

end module radint_m
