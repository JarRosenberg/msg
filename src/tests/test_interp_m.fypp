#:include 'forum.inc'

module test_interp_m

  ! Uses

  use forum_m
  use interp_m
  use order_m

  use ISO_FORTRAN_ENV

  ! No implicit typing

  implicit none

  ! Access specifiers

  private

  public :: test

contains
 
   subroutine test()

      call test_eval_kernels_()
      call test_bicubic_interp_()

   end subroutine test

   !****

   subroutine test_eval_kernels_()

      integer, parameter :: n_x = 4
      integer, parameter :: n_y = 6
      
      real(RD) :: x(n_x)
      real(RD) :: y(n_y)
      real(RD) :: f(n_x,n_y)
      real(RD) :: df_dx(n_x-2,n_y-2)
      real(RD) :: df_dy(n_x-2,n_y-2)
      real(RD) :: d2f_dxdy(n_x-2,n_y-2)
      real(RD) :: df_dx_chk(n_x-2,n_y-2)
      real(RD) :: df_dy_chk(n_x-2,n_y-2)
      real(RD) :: d2f_dxdy_chk(n_x-2,n_y-2)

      print *,'eval_kernels'

      call eval_kernels_arrange_()
      call eval_kernels_act_()
      call eval_kernels_assert_()

   contains      
      
      subroutine eval_kernels_arrange_()

         integer :: i
         integer :: j

         x = [(i-1,i=1,n_x)]
         y = [(j-1,j=1,n_y)]**2

         do i = 1, n_x
            do j = 1, n_y
               call eval_biquadratic_(x(i), y(j), f(i,j))
            end do
         end do

      end subroutine eval_kernels_arrange_

      !****
  
      subroutine eval_kernels_act_()

         integer  :: i
         integer  :: j
         integer  :: l
         integer  :: m
         real(RD) :: kern_1(3)
         real(RD) :: kern_2(3,3)
         real(RD) :: f_

         do i = 2, n_x-1
            do j = 2, n_y-1

               l = i-1
               m = j-1
               
               call eval_deriv_kernel(x(i-1:i+1), kern_1)
               df_dx(l,m) = SUM(kern_1*f(i-1:i+1,j))
               
               call eval_deriv_kernel(y(j-1:j+1), kern_1)
               df_dy(l,m) = SUM(kern_1*f(i,j-1:j+1))
               
               call eval_cross_kernel(x(i-1:i+1), y(j-1:j+1), kern_2)
               d2f_dxdy(l,m) = SUM(kern_2*f(i-1:i+1,j-1:j+1))
               
               call eval_biquadratic_(x(i), y(j), f_, df_dx_chk(l,m), df_dy_chk(l,m), d2f_dxdy_chk(l,m))

            end do
         end do

      end subroutine eval_kernels_act_

      !****
      
      subroutine eval_kernels_assert_()

         real(RD), parameter :: tol = 1E-13_RD

         real(RD), allocatable :: df_dx_err(:,:)
         real(RD), allocatable :: df_dy_err(:,:)
         real(RD), allocatable :: d2f_dxdy_err(:,:)

         df_dx_err = (df_dx - df_dx_chk)/df_dx_chk
         df_dy_err = (df_dy - df_dy_chk)/df_dy_chk
         d2f_dxdy_err = (d2f_dxdy - d2f_dxdy_chk)/d2f_dxdy_chk

         if (ALL(ABS(df_dx_err) < tol)) then
            print *,'  PASS df_dx'
         else
            print *,'  FAIL df_dx:', MAXVAL(ABS(df_dx_err)), '>', tol
         end if

         if (ALL(ABS(df_dy_err) < tol)) then
            print *,'  PASS df_dy'
         else
            print *,'  FAIL df_dy:', MAXVAL(ABS(df_dy_err)), '>', tol
         end if

         if (ALL(ABS(d2f_dxdy_err) < tol)) then
            print *,'  PASS d2f_dxdy'
         else
            print *,'  FAIL d2f_dxdy:', MAXVAL(ABS(d2f_dxdy_err)), '>', tol
         end if

      end subroutine eval_kernels_assert_

   end subroutine test_eval_kernels_

   !****

   subroutine test_bicubic_interp_()

      integer, parameter :: n_x = 4
      integer, parameter :: n_y = 6
      integer, parameter :: n_over = 10
      integer, parameter :: n_x_int = n_over*(n_x-1) + 1
      integer, parameter :: n_y_int = n_over*(n_y-1) + 1
      
      real(RD) :: x(n_x)
      real(RD) :: y(n_y)
      real(RD) :: f(n_x,n_y)
      real(RD) :: df_dx(n_x,n_y)
      real(RD) :: df_dy(n_x,n_y)
      real(RD) :: d2f_dxdy(n_x,n_y)
      real(RD) :: x_int(n_x_int)
      real(RD) :: y_int(n_y_int)
      real(RD) :: f_int(n_x_int,n_y_int)
      real(RD) :: df_dx_int(n_x_int,n_y_int)
      real(RD) :: df_dy_int(n_x_int,n_y_int)
      real(RD) :: d2f_dxdy_int(n_x_int,n_y_int)
      real(RD) :: f_chk(n_x_int,n_y_int)
      real(RD) :: df_dx_chk(n_x_int,n_y_int)
      real(RD) :: df_dy_chk(n_x_int,n_y_int)
      real(RD) :: d2f_dxdy_chk(n_x_int,n_y_int)

      print *,'bicubic_interp'

      call bicubic_interp_arrange_()
      call bicubic_interp_act_()
      call bicubic_interp_assert_()

   contains

      subroutine bicubic_interp_arrange_()

         integer :: i
         integer :: j

         x = [(i-1,i=1,n_x)]
         y = [(j-1,j=1,n_y)]

         do i = 1, n_x
            do j = 1, n_y
               call eval_bicubic_(x(i), y(j), f(i,j), df_dx(i,j), df_dy(i,j), d2f_dxdy(i,j))
            end do
         end do

      end subroutine bicubic_interp_arrange_

      !****
  
      subroutine bicubic_interp_act_()

         integer  :: i
         integer  :: j
         integer  :: l
         integer  :: m
         real(RD) :: dx
         real(RD) :: dy
         real(RD) :: u
         real(RD) :: v

         do i = 1, n_x-1
            do m = 1, n_over
               x_int(n_over*(i-1)+m) = x(i) + (x(i+1) - x(i))*(m-1)/n_over
            end do
         end do

         do j = 1, n_y-1
            do m = 1, n_over
               y_int(n_over*(j-1)+m) = y(j) + (y(j+1) - y(j))*(m-1)/n_over
            end do
         end do

         x_int(n_x_int) = x(n_x)
         y_int(n_y_int) = y(n_y)

         do i = 1, n_x_int
            do j = 1,n_y_int

               call locate(x, x_int(i), l)
               call locate(y, y_int(j), m)

               dx = x(l+1) - x(l)
               dy = y(m+1) - y(m)
               
               u = (x_int(i) - x(l))/dx
               v = (y_int(j) - y(m))/dy

               call bicubic_interp(f(l:l+1,m:m+1), df_dx(l:l+1,m:m+1), df_dy(l:l+1,m:m+1), d2f_dxdy(l:l+1,m:m+1), &
                    dx, dy, u, v, f_int(i,j))

               call bicubic_interp(f(l:l+1,m:m+1), df_dx(l:l+1,m:m+1), df_dy(l:l+1,m:m+1), d2f_dxdy(l:l+1,m:m+1), &
                    dx, dy, u, v, df_dx_int(i,j), d_dx=.TRUE.)

               call bicubic_interp(f(l:l+1,m:m+1), df_dx(l:l+1,m:m+1), df_dy(l:l+1,m:m+1), d2f_dxdy(l:l+1,m:m+1), &
                    dx, dy, u, v, df_dy_int(i,j), d_dy=.TRUE.)

               call bicubic_interp(f(l:l+1,m:m+1), df_dx(l:l+1,m:m+1), df_dy(l:l+1,m:m+1), d2f_dxdy(l:l+1,m:m+1), &
                    dx, dy, u, v, d2f_dxdy_int(i,j), d_dx=.TRUE., d_dy=.TRUE.)

               call eval_bicubic_(x_int(i), y_int(j), f_chk(i,j), df_dx_chk(i,j), df_dy_chk(i,j), d2f_dxdy_chk(i,j))

            end do
         end do

      end subroutine bicubic_interp_act_

      !****
      
      subroutine bicubic_interp_assert_()

         real(RD), parameter :: tol = 1E-13_RD

         real(RD), allocatable :: f_err(:,:)
         real(RD), allocatable :: df_dx_err(:,:)
         real(RD), allocatable :: df_dy_err(:,:)
         real(RD), allocatable :: d2f_dxdy_err(:,:)

         f_err = (f_int - f_chk)/f_chk
         df_dx_err = (df_dx_int - df_dx_chk)/f_chk
         df_dy_err = (df_dy_int - df_dy_chk)/f_chk
         d2f_dxdy_err = (d2f_dxdy_int - d2f_dxdy_chk)/f_chk

         if (ALL(ABS(f_err) < tol)) then
            print *,'  PASS f'
         else
            print *,'  FAIL f:', MAXVAL(ABS(f_err)), '>', tol
         end if

         if (ALL(ABS(df_dx_err) < tol)) then
            print *,'  PASS df_dx'
         else
            print *,'  FAIL df_dx:', MAXVAL(ABS(df_dx_err)), '>', tol
         end if

         if (ALL(ABS(df_dy_err) < tol)) then
            print *,'  PASS df_dy'
         else
            print *,'  FAIL df_dy:', MAXVAL(ABS(df_dy_err)), '>', tol
         end if

         if (ALL(ABS(d2f_dxdy_err) < tol)) then
            print *,'  PASS d2f_dxdy'
         else
            print *,'  FAIL d2f_dxdy:', MAXVAL(ABS(d2f_dxdy_err)), '>', tol
         end if


      end subroutine bicubic_interp_assert_

   end subroutine test_bicubic_interp_

   !****

   subroutine eval_biquadratic_(x, y, f, df_dx, df_dy, d2f_dxdy)

      real(RD), intent(in)            :: x
      real(RD), intent(in)            :: y
      real(RD), intent(out)           :: f
      real(RD), intent(out), optional :: df_dx
      real(RD), intent(out), optional :: df_dy
      real(RD), intent(out), optional :: d2f_dxdy

      f = x**2 + x*y**2 + 2*y + 4

      if (PRESENT(df_dx)) then
         df_dx = 2*x + y**2
      endif

      if (PRESENT(df_dy)) then
         df_dy = 2*x*y + 2
      endif

      if (PRESENT(d2f_dxdy)) then
         d2f_dxdy = 2*y
      endif

   end subroutine eval_biquadratic_

   !****

   subroutine eval_bicubic_(x, y, f, df_dx, df_dy, d2f_dxdy)

      real(RD), intent(in)            :: x
      real(RD), intent(in)            :: y
      real(RD), intent(out)           :: f
      real(RD), intent(out), optional :: df_dx
      real(RD), intent(out), optional :: df_dy
      real(RD), intent(out), optional :: d2f_dxdy

      f = x**3 + x*y**3 + 2*y**2 + 4

      if (PRESENT(df_dx)) then
         df_dx = 3*x**2 + y**3
      endif

      if (PRESENT(df_dy)) then
         df_dy = 3*x*y**2 + 4*y
      endif

      if (PRESENT(d2f_dxdy)) then
         d2f_dxdy = 3*y**2
      endif

   end subroutine eval_bicubic_

end module test_interp_m
 
