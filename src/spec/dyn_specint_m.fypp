! Module  : dyn_specint_m
! Purpose : define dyn_specint_t type, for adding dynamic loading to the
!           specint_t type
!
! Copyright 2021-2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module dyn_specint_m

   ! Uses

   use forum_m

   use specint_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(specint_t) :: dyn_specint_t
      private
      type(hdf5io_t) :: hi
      real(RD)       :: lam_min
      real(RD)       :: lam_max
      logical        :: loaded = .FALSE.
   contains
      private
      procedure, public :: inquire
      #:for name in ('intensity', 'D_moment', 'flux')
         procedure, public :: interp_${name}$
      #:endfor
      procedure         :: load_
   end type dyn_specint_t

   ! Interfaces

   interface read
      module procedure read_
   end interface read

   interface write
      module procedure write_
   end interface write

   ! Access specifiers

   private

   public :: dyn_specint_t
   public :: read
   public :: write

   ! Procedures

contains

   subroutine inquire(self, lam_min, lam_max)

      class(dyn_specint_t), intent(in) :: self
      real(RD), intent(out), optional  :: lam_min
      real(RD), intent(out), optional  :: lam_max

      ! Inquire metadata

      if (self%loaded) then

         call self%specint_t%inquire(lam_min, lam_max)

      else

         if (PRESENT(lam_min)) lam_min = self%lam_min
         if (PRESENT(lam_max)) lam_max = self%lam_max

      endif

      ! Finish

      return

   end subroutine inquire

   !****

   #:for name, var, arg_var, arg_type in (('intensity', 'I', 'mu', 'real(RD)'), &
                                         ('D_moment', 'D', 'l', 'integer'))

      subroutine interp_${name}$(self, ${arg_var}$, lam, ${var}$, stat)

         class(dyn_specint_t), intent(inout) :: self
         ${arg_type}$, intent(in)            :: ${arg_var}$
         real(RD), intent(in)                :: lam(:)
         real(RD), intent(out)               :: ${var}$(:)
         integer, intent(out), optional      :: stat

         ! If necessary, load

         if (.NOT. self%loaded) call self%load_()
         
         ! Pass

         call self%specint_t%interp_${name}$(${arg_var}$, lam, ${var}$, stat)

         ! Finish

         return

      end subroutine interp_${name}$

   #:endfor
      
   !****

   #:for name, var in (('flux', 'F'),)

      subroutine interp_${name}$(self, lam, ${var}$, stat)

         class(dyn_specint_t), intent(inout) :: self
         real(RD), intent(in)                :: lam(:)
         real(RD), intent(out)               :: ${var}$(:)
         integer, intent(out), optional      :: stat

         ! If necessary, load

         if (.NOT. self%loaded) call self%load_()
         
         ! Pass

         call self%specint_t%interp_${name}$(lam, ${var}$, stat)

         ! Finish

         return

      end subroutine interp_${name}$

   #:endfor

   !****

   subroutine load_(self)
      
      class(dyn_specint_t), intent(inout) :: self

      real(RD) :: lam_min
      real(RD) :: lam_max

      ! Load the specint

      call read(self%hi, self%specint_t)
      call self%hi%final()

      self%loaded = .TRUE.

      ! Consistency check

      call self%inquire(lam_min, lam_max)

      @:ASSERT(lam_min == self%lam_min, 'inconsistent lam_min')
      @:ASSERT(lam_max == self%lam_max, 'inconsistent lam_max')

      ! Finish

      return

   end subroutine load_

   !****

   subroutine read_(hi, si)

      type(hdf5io_t), intent(inout)    :: hi
      type(dyn_specint_t), intent(out) :: si

      ! Read the dyn_specint_t (the specint_t supertype is set in a
      ! later call to load_)

      si%hi = hi

      call hi%read_attr('lam_min', si%lam_min)
      call hi%read_attr('lam_max', si%lam_max)

      ! Finish

      return

   end subroutine read_

   !****

   subroutine write_(hi, si)

      type(hdf5io_t), intent(inout)      :: hi
      type(dyn_specint_t), intent(inout) :: si

      ! Write the dyn_specint_t

      if (.NOT. si%loaded) call si%load_()
         
      call write(hi, si%specint_t)

      ! Finish

      return

   end subroutine write_
   
end module dyn_specint_m
