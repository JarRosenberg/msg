! Module  : hdf5_spectable_m
! Purpose : define hdf5_spectable_t type, for representing a cached
!           table of spectral intensities
!
! Copyright 2022 Rich Townsend & The MSG Team
!
! This file is part of MSG. MSG is free software: you can redistribute
! it and/or modify it under the terms of the GNU General Public
! License as published by the Free Software Foundation, version 3.
!
! MSG is distributed in the hope that it will be useful, but WITHOUT
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
! or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
! License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

#:include 'forum.inc'

module hdf5_spectable_m

   ! Uses

   use forum_m

   use specint_m
   use spectable_m
   use stat_m

   use ISO_FORTRAN_ENV

   ! No implicit typing

   implicit none

   ! Derived-type definitions

   type, extends(spectable_t) :: hdf5_spectable_t
      private
      type(specint_t), allocatable :: si(:)
      character(:), allocatable    :: file_names(:)
      character(:), allocatable    :: group_names(:)
      logical, allocatable         :: loaded(:)
      real(RD)                     :: lam_min
      real(RD)                     :: lam_max
      integer                      :: n
      logical                      :: sticky
   contains
      private
      procedure, public :: inquire
      procedure, public :: fetch
      procedure         :: flush_
      procedure         :: load_
   end type hdf5_spectable_t

   ! Interfaces

   interface hdf5_spectable_t
      module procedure hdf5_spectable_t_file_
      module procedure hdf5_spectable_t_files_
   end interface hdf5_spectable_t

   ! Access specifiers

   private

   public :: hdf5_spectable_t

contains

   function hdf5_spectable_t_file_(file_name, group_names, lam_min, lam_max, sticky) result(st)

      character(*), intent(in)      :: file_name
      character(*), intent(in)      :: group_names(:)
      real(RD), intent(in)          :: lam_min 
      real(RD), intent(in)          :: lam_max
      logical, intent(in), optional :: sticky
      type(hdf5_spectable_t)        :: st

      integer                   :: n
      character(:), allocatable :: file_names(:)

      ! Construct the spectable_t from the file name and list of group names

      n = SIZE(group_names)

      allocate(character(LEN(file_name))::file_names(n))

      file_names = file_name

      st = hdf5_spectable_t(file_names, group_names, lam_min, lam_max, sticky)

      ! Finish

      return

   end function hdf5_spectable_t_file_

   !****

   function hdf5_spectable_t_files_(file_names, group_names, lam_min, lam_max, sticky) result(st)

      character(*), intent(in)      :: file_names(:)
      character(*), intent(in)      :: group_names(:)
      real(RD), intent(in)          :: lam_min 
      real(RD), intent(in)          :: lam_max
      logical, intent(in), optional :: sticky
      type(hdf5_spectable_t)        :: st

      logical :: sticky_
      integer :: n

      @:CHECK_BOUNDS(SIZE(group_names), SIZE(file_names))

      if (PRESENT(sticky)) then
         sticky_ = sticky
      else
         sticky_ = .TRUE.
      endif

      ! Construct the spectable_t from the lists of file/group names

      n = SIZE(file_names)

      allocate(st%si(n))

      st%file_names = file_names
      st%group_names = group_names

      allocate(st%loaded(n))
      st%loaded = .FALSE.

      st%lam_min = lam_min
      st%lam_max = lam_max

      st%n = n

      st%sticky = sticky_

      ! Finish

      return

   end function hdf5_spectable_t_files_

   !****

   subroutine inquire(self, n, lam_min, lam_max)

      class(hdf5_spectable_t), intent(in) :: self
      integer, intent(out), optional      :: n
      real(RD), intent(out), optional     :: lam_min
      real(RD), intent(out), optional     :: lam_max

      ! Inquire metadata

      if (PRESENT(n)) n = self%n

      if (PRESENT(lam_min)) lam_min = self%lam_min
      if (PRESENT(lam_max)) lam_max = self%lam_max

      ! Finish

      return

   end subroutine inquire

   !****

   subroutine fetch(self, i, si, stat)

      class(hdf5_spectable_t), intent(inout) :: self
      integer, intent(in)                    :: i
      type(specint_t), intent(out)           :: si
      integer, intent(out), optional         :: stat

      ! If necessary, load

      if (.NOT. self%loaded(i)) then

         call self%load_(i, stat)
         if (PRESENT(stat)) then
            if (stat /= STAT_OK) return
         endif
         
      endif

      ! Fetch the specint_t
         
      si = self%si(i)

      ! If necesarry, flush

      if (.NOT. self%sticky) call self%flush_(i)

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine fetch

   !****

   subroutine flush_(self, i)

      class(hdf5_spectable_t), intent(inout) :: self
      integer, intent(in)                    :: i

      ! Flush a specint_t from the table

      call flush_si_(self%si(i))

      self%loaded(i) = .FALSE.

      ! Finish

      return

   contains

      subroutine flush_si_(si)

         type(specint_t), intent(out) :: si

      end subroutine flush_si_

   end subroutine flush_
      
   !****

   subroutine load_(self, i, stat)
      
      class(hdf5_spectable_t), intent(inout) :: self
      integer, intent(in)                    :: i
      integer, intent(out), optional         :: stat

      type(hdf5io_t) :: hi
      type(hdf5io_t) :: hi_si

      ! Load the specint_t into the table

      hi = hdf5io_t(self%file_names(i), OPEN_FILE_RO)
      hi_si = hdf5io_t(hi, self%group_names(i))

      call read(hi_si, self%si(i))

      call hi_si%final()
      call hi%final()

      self%loaded(i) = .TRUE.

      ! Finish

      if (PRESENT(stat)) stat = STAT_OK

      return

   end subroutine load_

end module hdf5_spectable_m
